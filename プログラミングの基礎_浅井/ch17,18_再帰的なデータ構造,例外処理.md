# 17章

## 問題17.1

- 第八版だと、"let to_seireki : nengou = match nengou with"となってるが、おそらくは、正しくはコロンが不要
- 関数呼び出し時に、カッコを付けないと型違反で怒られる。to_seireki (Meiji 10)
  http://pllab.is.ocha.ac.jp/~asai/book-mov/a.pdf


```ocaml
type nengou_t = Meiji of int
              | Taisho of int
              | Showa of int
              | Heisei of int;;

let to_seireki nengou = match nengou with
    Meiji (n) -> n + 1867
  | Taisho (n) -> n + 1911
  | Showa (n) -> n + 1925
  | Heisei (n) -> n + 1988;;

let nenrei nengou1 nengou2 =
  to_seireki nengou2 - to_seireki nengou1;;

let test1 = nenrei (Showa (59)) (Heisei (33)) = 37

```

## 問題17.2, 17.3, 17.4

```ocaml
type year_t = January   of int
            | February  of int
            | March     of int
            | April     of int
            | May       of int
            | June      of int
            | July      of int
            | August    of int
            | September of int
            | October   of int
            | November  of int
            | December  of int

type seiza_t = Capricorus	(* 山羊座 *)
             | Aquarius		(* 水瓶座 *)
             | Pisces		(* 魚座 *)
             | Aries		(* 牡羊座 *)
             | Taurus		(* 牡牛座 *)
             | Gemini		(* 双子座 *)
             | Cancer		(* 蟹座 *)
             | Leo		(* 獅子座 *)
             | Virgo		(* 乙女座 *)
             | Libra		(* 天秤座 *)
             | Scorpius		(* 蠍座 *)
             | Sagittarius	(* 射手座 *)

let seiza year       = match year with
                         January   (n) -> if n >= 20 && n <= 31 then Aquarius    else Capricorus
                       | February  (n) -> if n >= 19 && n <= 30 then Pisces      else Aquarius
                       | March     (n) -> if n >= 21 && n <= 31 then Aries       else Pisces
                       | April     (n) -> if n >= 20 && n <= 30 then Taurus      else Aries
                       | May       (n) -> if n >= 21 && n <= 31 then Gemini      else Taurus
                       | June      (n) -> if n >= 22 && n <= 30 then Cancer      else Gemini
                       | July      (n) -> if n >= 23 && n <= 31 then Leo         else Cancer
                       | August    (n) -> if n >= 23 && n <= 31 then Virgo       else Leo
                       | September (n) -> if n >= 23 && n <= 30 then Libra       else Virgo
                       | October   (n) -> if n >= 24 && n <= 31 then Scorpius    else Libra
                       | November  (n) -> if n >= 23 && n <= 30 then Sagittarius else Scorpius
                       | December  (n) -> if n >= 22 && n <= 31 then Capricorus  else Sagittarius;;

let test1 = seiza (January (20)) = Aquarius;;
let test2 = seiza (December (31)) = Capricorus;;

(* let seiza month data = if       month == 1  then if data >= 20 && data <= 31 then "水瓶"  else "山羊"
                       else if  month == 2  then if data >= 19 && data <= 30 then "魚"    else "水瓶"
                       else if  month == 3  then if data >= 21 && data <= 31 then "牡羊"  else "魚"
                       else if  month == 4  then if data >= 20 && data <= 30 then "牡牛"   else "牡羊"
                       else if  month == 5  then if data >= 21 && data <= 31 then "双子"  else "牡牛"
                       else if  month == 6  then if data >= 22 && data <= 30 then "蟹"    else "双子"
                       else if  month == 7  then if data >= 23 && data <= 31 then "獅子"  else "蟹"
                       else if  month == 8  then if data >= 23 && data <= 31 then "乙女"   else "獅子"
                       else if  month == 9  then if data >= 23 && data <= 30 then "天秤"  else "乙女"
                       else if  month == 10 then if data >= 24 && data <= 31 then "蠍"    else "天秤"
                       else if  month == 11 then if data >= 23 && data <= 30 then "射手" else "蠍"
                       else if  month == 12 then if data >= 22 && data <= 31 then "山羊"  else "射手"
                       else "INVALID MONTH" *)

```

## 問題17.5

```ocaml
type tree_t = Empty                     (* 空の木 *)
            | Leaf of int                   (* 葉 *)
            | Node of tree_t * int * tree_t (* 節 *)

let tree1 = Empty
let tree2 = Leaf (3)
let tree3 = Node (tree1, 4, tree2)
let tree4 = Node (tree2, 5, tree3)

let rec sum_tree tree = match tree with
    Empty -> 0
  | Leaf (n) -> n
  | Node (t1, n, t2) -> sum_tree t1 + n + sum_tree t2

let rec tree_double tree = match tree with
    Empty -> Empty
  | Leaf (n) -> Leaf (2 * n)
  | Node (t1, n, t2) -> Node (tree_double t1, 2 * n, tree_double t2)

let test_sum1 = sum_tree tree1 = 0
let test_sum2 = sum_tree tree2 = 3
let test_sum3 = sum_tree tree3 = 7
let test_sum4 = sum_tree tree4 = 15

let test1 = tree_double tree1 = Empty
let test2 = tree_double tree2 = Leaf (6)
let test3 = tree_double tree3 = Node (Empty, 8, Leaf (6))
let test4 = tree_double tree4 = Node (Leaf (6), 10, Node (Empty, 8, Leaf (6)))
```

## 問題17.6

```ocaml
let rec tree_map tree f = match tree with
    Empty -> Empty
  | Leaf (n) -> Leaf (f n)
  | Node (t1, n, t2) -> Node (tree_map t1 f, f n, tree_map t2 f)

let double n = n * 2

let test1 = tree_map tree1 double = Empty
let test2 = tree_map tree2 double = Leaf (6)
let test3 = tree_map tree3 double = Node (Empty, 8, Leaf (6))
let test4 = tree_map tree4 double = Node (Leaf (6), 10, Node (Empty, 8, Leaf (6)))

```

## 問題17.7

```ocaml

let rec tree_length tree= match tree with
    Empty -> 0
  | Leaf (n) -> 1
  | Node (t1, n, t2) -> (tree_length t1) + 1 + (tree_length t2)


let test1 = tree_length tree1 = 0
let test2 = tree_length tree2 = 1
let test3 = tree_length tree3 = 2
let test4 = tree_length tree4 = 4

```


## 問題17.8

```ocaml
let rec tree_depth tree= match tree with
    Empty -> 0
  | Leaf (n) -> 0
  | Node (t1, n, t2) -> 1 + max (tree_depth t1) (tree_depth t2)

let test1 = tree_depth tree1 = 0
let test2 = tree_depth tree2 = 0
let test3 = tree_depth tree3 = 1
let test4 = tree_depth tree4 = 2

```

## 問題17.9

任意型の2分木、教科書の写し。

```ocaml

type 'a tree_t = Empty                          (* 空の木 *)
               | Leaf of 'a                         (* 葉 *)
               | Node of 'a tree_t * 'a * 'a tree_t (* 節 *)

let rec search tree data = match tree with
    Empty -> false
  | Leaf (n) -> data = n
  | Node (t1, n, t2) ->
      if data = n then true
      else if data < n then search t1 data
      else search t2 data

let rec insert_tree tree data = match tree with
    Empty -> Leaf (data)
  | Leaf (n) ->
      if data = n then Leaf (n)
      else if data < n then Node (Leaf (data), n, Empty)
                       else Node (Empty, n, Leaf (data))
  | Node (t1, n, t2) ->
      if data = n then Node (t1, n, t2)
      else if data < n then Node (insert_tree t1 data, n, t2)
                       else Node (t1, n, insert_tree t2 data)

let tree1 = Empty
let tree2 = Leaf (3)
let tree3 = Node (Leaf (1), 2, Leaf (3))
let tree4 = Node (Empty, 7, Leaf (9))
let tree5 = Node (tree3, 6, tree4)

let test1 = insert_tree Empty 3 = Leaf (3)
let test2 = insert_tree (Leaf (3)) 2 = Node (Leaf (2), 3, Empty)
let test3 = insert_tree (Leaf (3)) 3 = Leaf (3)
let test4 = insert_tree (Leaf (3)) 4 = Node (Empty, 3, Leaf (4))
let test5 = insert_tree tree5 4 =
            Node (Node (Leaf (1), 2, Node (Empty, 3, Leaf (4))),
                  6,
                  Node (Empty, 7, Leaf (9)))

```

```ocaml

let rec sum_tree tree = match tree with
    Empty -> 0
  | Leaf (n) -> n
  | Node (t1, n, t2) -> sum_tree t1 + n + sum_tree t2

```

関数の型はそのまま。sum_treeが、nの整数和を求める式になっているため、多相にならない。

```
val sum_tree : int tree_t -> int = <fun>
```


## 問題17.10, 17.11

- Leafは、左も右もEmptyであるNodeを代用する
- Nodeは、駅名(自分の)と接続先の駅と距離のリストを持つ

```ocaml
type ekikan_tree_t =
    Empty
  | Node of ekikan_tree_t * string * (string * float) list * ekikan_tree_t


let rec assoc ekimei0 lst = match lst with
    [] -> infinity
  | (ekimei, kyori) :: rest ->
      if ekimei = ekimei0 then kyori else assoc ekimei0 rest

let test1 = assoc "後楽園" [] = infinity
let test2 = assoc "後楽園" [("新大塚", 1.2); ("後楽園", 1.8)] = 1.8
let test3 = assoc "池袋" [("新大塚", 1.2); ("後楽園", 1.8)] = infinity

```

## 問題17.12, 17.13

```ocaml
(* 片方向のデータを挿入する関数 *)
let rec insert1 ekikan_tree kiten shuten kyori = match ekikan_tree with
    Empty -> Node (Empty, kiten, [(shuten, kyori)], Empty)
  | Node (left, ekimei, lst, right) ->
      if kiten < ekimei      then Node (insert1 left kiten shuten kyori, ekimei, lst,                    right)
      else if ekimei < kiten then Node (left,                            ekimei, lst,                    insert1 right kiten shuten kyori)
      else                        Node (left,                            ekimei, (shuten, kyori) :: lst, right)

(* ekikan_treeに、ekikanを両方向で挿入する。 *)
let insert_ekikan ekikan_tree ekikan = match ekikan with
  {kiten = kiten; shuten = shuten; keiyu = keiyu; kyori = kyori; jikan = jikan} ->
    insert1 (insert1 ekikan_tree shuten kiten kyori) kiten shuten kyori


let inserts_ekikan ekikan_tree ekikan_list =
  List.fold_right (fun ekikan tree -> insert_ekikan tree ekikan)
		ekikan_list ekikan_tree



inserts_ekikan Empty [ekikan1; ekikan2; ekikan3];;

let test1 = inserts_ekikan Empty [ekikan1; ekikan2; ekikan3] =
  Node (Empty, "後楽園", [("茗荷谷", 1.8)],
    Node(
      Node (Empty, "新大塚", [("池袋", 1.8); ("茗荷谷", 1.2)],Node (Empty, "池袋", [("新大塚", 1.8)], Empty)),
      "茗荷谷",
      [("新大塚", 1.2); ("後楽園", 1.8)], Empty
    )
  );;


```


## 問題17.14

```ocaml
(* let rec get_ekikan_kyori kanji1 kanji2 lst = match lst with
    [] -> infinity
  | first :: rest ->
      if ( first.kiten = kanji1 && first.shuten = kanji2 ) ||
         ( first.kiten = kanji2 && first.shuten = kanji1 ) then first.kyori
      else get_ekikan_kyori kanji1 kanji2 rest *)

let rec get_ekikan_kyori kanji1 kanji2 tree = match tree with
    Empty -> infinity
  | Node (left, k, lst, right) ->
      if      kanji1 < k then get_ekikan_kyori kanji1 kanji2 left
      else if kanji1 > k then get_ekikan_kyori kanji1 kanji2 right
      else assoc kanji2 lst
```

## 問題17.15

- koushinでのget_ekikan_kyoriをtreeから行う。
- dijkstra_mainを、treeから行う。
- dijkstraで、global_ekikan_treeを作る。


速度計測は未。すいません。。。

```ocaml
let koushin p v ekikan_tree = match p with
  {namae = pn; saitan_kyori = ps; temae_list = pt} ->
    List.map (fun q -> match q with
	                      {namae = qn; saitan_kyori = qs; temae_list = qt} ->
                    		  let kyori = get_ekikan_kyori pn qn ekikan_tree in
		                        if kyori = infinity then q
		                        else if ps +. kyori < qs then {namae = qn; saitan_kyori = ps +. kyori; temae_list = qn :: pt}
		                        else q)
	            v

let rec dijkstra_main lst ekikan_tree = match lst with
    [] -> []
  | lst -> let (p, v) = saitan_wo_bunri lst in
    p :: dijkstra_main (koushin p v ekikan_tree) ekikan_tree


let dijkstra shiten shuten =
  let shiten_kanji = romaji_to_kanji shiten global_ekimei_list in
  let shuten_kanji = romaji_to_kanji shuten global_ekimei_list in
  let eki_list = make_initial_eki_list global_ekimei_list shiten_kanji in
  let global_ekikan_tree = inserts_ekikan Empty global_ekikan_list in
  let saitan_list = dijkstra_main eki_list global_ekikan_tree in
  let rec get_eki lst eki = match lst with
      [] -> {namae=""; saitan_kyori=infinity; temae_list = []};
    | first :: rest -> if first.namae = shuten_kanji
                       then first
                       else get_eki rest eki in
   get_eki saitan_list shuten_kanji
```


## 問題17.16, 17.17

```ocaml

(* 元々書いていたsaitan_wo_bunri *)
let rec saitan_wo_bunri lst = match lst with
    [] -> ( {namae = "";  saitan_kyori = infinity; temae_list = []}, [] )
  | first :: rest ->
      let rest_res = saitan_wo_bunri rest in
      match rest_res with (min_rest, rest_rest) ->
      if first.saitan_kyori < min_rest.saitan_kyori then (first,    List.filter (fun ele -> not (ele.namae = first.namae)) lst)
                                                    else (min_rest, List.filter (fun ele -> not (ele.namae = min_rest.namae)) lst)

(* Listが空の時に、駅名と[]を返す。*)
let rec saitan_wo_bunri eki lst = match lst with
    (* [] -> ( {namae = "";  saitan_kyori = infinity; temae_list = []}, [] ) *)
  [] -> (eki, [])
  | first :: rest ->
      let rest_res = saitan_wo_bunri first rest in
      match rest_res with (min_rest, rest_rest) ->
      if first.saitan_kyori < min_rest.saitan_kyori then (first,    List.filter (fun ele -> not (ele.namae = first.namae)) lst)
                                                    else (min_rest, List.filter (fun ele -> not (ele.namae = min_rest.namae)) lst)

```


## 問題18.1

```ocaml

type person_t = {
  namae : string;
  m : float;
  kg : float;
  birth  : string;
  blood : string;
}

let rec first_A lst = match lst with
    [] -> None
  | first :: rest -> match first with
      {namae = n; blood = b} ->
        if b = "A" then Some (first) else first_A rest


let watasi = {
  namae = "Taro Yamada";
  m = 1.67;
  kg = 62.0 ;
  birth  = "2020/1/21";
  blood = "A";
}

let anata = {
  namae = "Shiro Amada";
  m = 1.67;
  kg = 62.0 ;
  birth  = "2020/1/21";
  blood = "B";
}

let dareka = {
  namae = "Goro Hamada";
  m = 1.67;
  kg = 62.0 ;
  birth  = "2020/1/21";
  blood = "O";
};;

let persons = [watasi ; anata; dareka ];;

first_A persons;;

let persons = [anata; dareka ];;
first_A persons;;

```


## 問題18.2

```ocaml

let yaoya_list = [("トマト", 300); ("たまねぎ", 200);
                  ("にんじん", 150); ("ほうれん草", 200)]

let rec price item yaoya_list = match yaoya_list with
    [] -> None
  | (yasai, nedan) :: rest ->
      if item = yasai then Some (nedan)
                      else price item rest

let rec count_urikire_yasai yasai_list yaoya_list = match yasai_list with
    [] -> 0
  | first :: rest ->
      match price first yaoya_list with
    	    None -> 1 + count_urikire_yasai rest yaoya_list
	      | Some (p) -> count_urikire_yasai rest yaoya_list
```


## 問題18.3

```ocaml
(* let rec assoc ekimei0 lst = match lst with
    [] -> infinity
  | (ekimei, kyori) :: rest ->
      if ekimei = ekimei0 then kyori else assoc ekimei0 rest *)

let rec assoc ekimei0 lst = match lst with
    [] -> raise Not_found
  | (ekimei, kyori) :: rest ->
      if ekimei = ekimei0 then kyori else assoc ekimei0 rest
```


## 問題18.4, 18.5

```ocaml
(* let rec get_ekikan_kyori kanji1 kanji2 tree = match tree with
    Empty -> infinity
  | Node (left, k, lst, right) ->
      if      kanji1 < k then get_ekikan_kyori kanji1 kanji2 left
      else if kanji1 > k then get_ekikan_kyori kanji1 kanji2 right
      else assoc kanji2 lst *)

let rec get_ekikan_kyori kanji1 kanji2 tree = match tree with
    Empty -> raise Not_found
  | Node (left, k, lst, right) ->
      if      kanji1 < k then get_ekikan_kyori kanji1 kanji2 left
      else if kanji1 > k then get_ekikan_kyori kanji1 kanji2 right
      else assoc kanji2 lst


(* let koushin p v ekikan_tree = match p with
  {namae = pn; saitan_kyori = ps; temae_list = pt} ->
    List.map (fun q -> match q with
	                      {namae = qn; saitan_kyori = qs; temae_list = qt} ->
                    		  let kyori = get_ekikan_kyori pn qn ekikan_tree in
		                        if kyori = infinity then q
		                        else if ps +. kyori < qs then {namae = qn; saitan_kyori = ps +. kyori; temae_list = qn :: pt}
		                        else q)
	            v *)

let koushin p v ekikan_tree = match p with
  {namae = pn; saitan_kyori = ps; temae_list = pt} ->
    List.map (fun q -> match q with
	                      {namae = qn; saitan_kyori = qs; temae_list = qt} ->
                          try
                      		  let kyori = get_ekikan_kyori pn qn ekikan_tree in
  		                        if kyori = infinity then q
  		                        else if ps +. kyori < qs then {namae = qn; saitan_kyori = ps +. kyori; temae_list = qn :: pt}
  		                        else q
                          with Not_found -> q)
	            v

```

## 問題18.6, 18.7

```ocaml

exception No_such_station of string

let rec romaji_to_kanji romaji lst = match lst with
    [] -> raise (No_such_station (romaji))
  | first :: rest ->
      if first.romaji = romaji then first.kanji
      else romaji_to_kanji romaji rest
```